apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.github.ben-manes.versions'

group = 'com.evernote'
version = project.VERSION_NAME

configurations {
  javadocDeps
}

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode Integer.parseInt(project.VERSION_CODE)
    versionName project.VERSION_NAME
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  compile 'eu.chainfire:libsuperuser:1.0.0.+'
  compile 'com.squareup.okhttp:okhttp:2.4.0'
  compile 'com.android.support.test:runner:0.3'
  compile 'com.android.support.test:rules:0.3'
  compile 'com.android.support.test.espresso:espresso-core:2.2'
  javadocDeps 'com.android.support.test.espresso:espresso-core:2.2'
}

task initArtifactoryPomPath << {
  artifactoryPublish {
    mavenDescriptor = "build/${project.name}-${project.version}.pom"
  }
}

model {
  tasks.generatePomFileForMavenPublication {
    destination = "build/${project.name}-${project.version}.pom"
  }
}

publishing {
  publications {
    maven(MavenPublication){
      groupId project.group
      artifactId project.name
      version project.version
      artifact("${buildDir}/outputs/aar/${project.name}-release.aar")
    }
  }
}

apply from: '../build-config/gradle-push.gradle'